plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.8.19'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'io.github.shreyashsaitwal'
version = '1.0.0'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': 'com.google.devtools.build.android.desugar.Desugar'
    }
}

shadowJar {
    exclude '*.proto'
    archiveClassifier.set('')
    minimize()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.3'
    implementation 'org.ow2.asm:asm-commons:9.3'
    implementation 'org.ow2.asm:asm-tree:9.3'
    implementation 'com.google.errorprone:error_prone_annotations:2.15.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.google.protobuf:protobuf-java:3.21.6'
    implementation 'com.google.auto.value:auto-value-annotations:1.9'
    annotationProcessor 'com.google.auto.value:auto-value:1.8.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.properties['ossrh.username']
                password = project.properties['ossrh.password']
            }
        }
    }

    publications {
        mavenJava(MavenPublication) { pub ->
            project.shadow.component(pub)
            pom {
                name = 'Java Desugar tool'
                description = 'Fork of the original desugar tool from Android: https://android.googlesource.com/platform/external/desugar/'
                url = 'https://github.com/shreyashsaitwal/desugar-tool'
                licenses {
                    license {
                        name = 'Apache License'
                        url = 'https://android.googlesource.com/platform/external/desugar/+/refs/heads/master/NOTICE'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
